plugins {
    id 'com.android.library'
}

android {
    namespace 'org.opencv'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_shared"
                cppFlags "-frtti -fexceptions"
            }
        }

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }

    ndkVersion "29.0.13846066"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'java/AndroidManifest.xml'
            java.srcDirs = ['java/src']
            resources.srcDirs = ['java/src']
            res.srcDirs = ['java/res']
            jniLibs.srcDirs = ['native/libs']
        }
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        pickFirst '**/libopencv_java4.so'
    }
}

tasks.register('cleanJniLibs') {
    doLast {
        delete fileTree('../app/src/main/jniLibs/') {
            include '**/*.so'
        }
    }
}

tasks.register('copyOpenCVLibs') {
    dependsOn cleanJniLibs
    doLast {
        ['arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'].each { abi ->
            def targetDir = file("../app/src/main/jniLibs/${abi}/")
            targetDir.mkdirs()

            copy {
                from("native/libs/${abi}")
                include "*.so"
                into(targetDir)
                fileMode 0644
            }
        }
    }
}

tasks.named('clean') {
    dependsOn('cleanJniLibs')
}

preBuild.dependsOn(copyOpenCVLibs)

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}
